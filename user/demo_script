% Config params, overwrite any previous settings from the commandline
config ap_ssid 		MyAP
config ap_password	stupidPassword
config ntp_server	1.pool.ntp.org
config mqtt_host	192.168.1.20

% Now the initialization, this is done once after booting
on init
do
	println "MQTT Script 1.0 starting"
	subscribe local /test/#
	settimer 1 1000			% once per second
	setvar $1=0
	setvar $2=0
	setvar $3=10
	gpio_pinmode 5 input pullup	% configure GPIO 5 as input

% Now the events, checked whenever something happens

% Here a remote re-publish, of any local topic starting with "/test/"
on topic local /test/#
do 
	publish remote $this_topic $this_data

% Now a check for local GPIOs
on gpio_interrupt 4 pullup
do
	println "New state GPIO 4: " | $this_gpio
	publish local /t/gpio4 $this_gpio

% When timer 1 expires, do some stuff
on timer 1
do
	% publish the current status of GPIO 5 as two byte binary val
	if gpio_in(5)=0 then
		publish local /t/gpio5 #0000
	endif
	if gpio_in(5)=1 then
		publish local /t/gpio5 #0001
	endif

	% Let the LED on GPIO 2 blink
	gpio_out 2 $1
	setvar $1 = not($1)

	% Count timer 1 ticks in var $2
	setvar $2=$2+1

	% And each time if we have reached 10, print that to the console
	if $2 = $3 then
		println "We have reached "|$2| " at " |$timestamp
		setvar $3=$2+10
	endif

	% Reload the timer
	settimer 1 1000

% Here a local publication once each day at noon
on clock 12:00:00
do
	publish local /t/2 "High Noon"
